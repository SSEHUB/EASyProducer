grammar de.uni_hildesheim.sse.Ivml hidden(WS, ML_COMMENT, SL_COMMENT)

generate ivml "http://www.uni_hildesheim.de/sse/Ivml"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

VariabilityUnit:
	{VariabilityUnit}
	projects+=Project*;

Project:
	'project' name=Identifier 
	'{'
     //#> Interfaces
      version=VersionStmt?
      imports+=ImportStmt*
      conflicts+=ConflictStmt*
	  interfaces += InterfaceDeclaration*
	  //#< Interfaces
	  contents=ProjectContents
	'}' ';'?
	;

ProjectContents:
	{ProjectContents} // ensure object creation
 	(   
 	    // in case of new elements adjust Utils.split
        elements+=Typedef 
		| elements+=VariableDeclaration 
		//#> EvalFreeze
		| elements+=Freeze 
		| elements+=Eval 
		//#< EvalFreeze
		| elements+=ExpressionStatement
		//#> Compounds
		| elements+=AnnotateTo
		//#< Compounds
		| elements+=OpDefStatement
		| elements+=AttrAssignment
	)*
;

Typedef:
	tEnum=TypedefEnum 
	| tCompound=TypedefCompound 
	| tMapping=TypedefMapping
;

TypedefEnum: 
	'enum' 
	name=Identifier 
	'{' 
		literals+=TypedefEnumLiteral 
		(
			','
			literals+=TypedefEnumLiteral
		)*
	'}'
	constraint=TypedefConstraint?
	';'
	;

TypedefEnumLiteral: 
	name=Identifier 
	(
		'=' value = NumValue
	)?
	;	

TypedefCompound: 
    (abstract = 'abstract')? 'compound' 
    name=Identifier 
    //#> Compounds
    (
    	'refines' 
    	super+=Identifier (',' super+=Identifier)*
    )? 
    //#< Compounds
    '{'
    	(elements+=VariableDeclaration 
    	    | elements+=ExpressionStatement 
    	    | elements+=AttrAssignment
    	    | elements+=Eval
    	)*
    '}' ';'?
	;

AttrAssignment:
    'assign' '(' parts+=AttrAssignmentPart (',' parts+=AttrAssignmentPart)* ')' 'to' '{'
        (elements+=VariableDeclaration | elements+=ExpressionStatement | elements+=AttrAssignment)+
    '}' ';'?
;

AttrAssignmentPart:
    name=Identifier '=' value=LogicalExpression
;
	
TypedefMapping: 
	'typedef' 
	newType=Identifier 
	type=Type 
	constraint=TypedefConstraint?
	';'
	;	

TypedefConstraint:
	'with' 
	'('
		expressions = Expression 
	')'
	;

VariableDeclaration : 
    const='const'?
	type=Type 
	decls+=VariableDeclarationPart 
	(
		',' 
		decls+=VariableDeclarationPart
	)*
	';'
	;

VariableDeclarationPart :	
	name=Identifier ('=' default=Expression)?
	;	

BasicType : 
	type='Integer' 
	| type='Real' 
	| type='Boolean' 
	| type='String'
    | type='Constraint'
    | type='Any'
	;

Type : 
	type=BasicType 
	| id=QualifiedName
	| derived=DerivedType
	;	

// just return string
NumValue :	
	val=NUMBER
	;
	
QualifiedName :	
	(
		qName+=Identifier 
		qName+='::' 
		(
			qName+=Identifier 
			qName+='::'
		)*
	)?
	qName+=Identifier
	;

AccessName:
	(aName+='.' aName+=Identifier)+
;

Value :	
	nValue = NumValue
	| sValue = STRING
	| qValue = QualifiedName
	| bValue = ('true' | 'false')
	| self = 'self'
	//#> Compounds
	| nullValue = 'null'
	| tValue = Type
	| => version = VERSION
	//#< Compounds
	;
	
DerivedType : 
	(
		op='setOf' 
		| op='sequenceOf'
		//#> Compounds
		| op='refTo'
		//#< Compounds
	) 
	'(' 
	type=Type 
	')' 
	;

//#> Compounds
// --------------------- Extension: Compounds ----------------------

AnnotateTo :
    sname=('attribute' | 'annotate')
	annotationType=Type
	
	annotationDecl=VariableDeclarationPart
	'to'
	(names+='.' | (names+=Identifier (',' names += Identifier)*))
	';'
	;
//#< Compounds

//#> EvalFreeze
// --------------------- Extension: EvalFreeze ---------------------

Freeze: 
	'freeze' '{'
	names+=(FreezeStatement)+
	'}' ('but' '(' id=Identifier '|' ex=LogicalExpression ')')? ';'?
	; 
	
FreezeStatement:
	(dot='.' | (name=QualifiedName access=AccessName?))
	';'	
;
	
Eval: 
    {Eval}
	'eval' 
    '{'
    nested+=Eval*
    statements+=ExpressionStatement* 
    '}' ';'?
	;	
	
//#< EvalFreeze
 
 
//#> Interfaces
// ---------------------- Extension: Interfaces ---------------------

InterfaceDeclaration :	
	'interface' 
	name=Identifier 
	'{'
		exports+=Export*
	'}'';'?
	;
		
Export :
	'export' names+=QualifiedName (',' names+=QualifiedName)* ';'
;		
	
ImportStmt: 
	'import' 
	name=Identifier ('::' interface=Identifier)?
	(
	  'with' restriction = Expression
	)?
	';'
	;

ConflictStmt: 
	'conflicts' 
	name=Identifier
	(
	  'with' restriction = Expression
	)?
	';'
	;
		
VersionStmt: 
	'version'
	version=VERSION
	';'
	;

//#< Interfaces

//------------------------------------ OCL (excerpt) ----------------------------	

OpDefStatement:
	'def' static='static'? result=Type id=Identifier 
	'(' param=OpDefParameterList ')' 
	'=' (impl=Expression ';' | block=BlockExpression)
;

OpDefParameterList:
	{OpDefParameterList}
    (list+=OpDefParameter
	(
		',' 
		list+=OpDefParameter
	)*)?
;

OpDefParameter:
	type=Type id=Identifier ('=' val=Expression)?
;

// do not rename this rule (fragment parsing)
ExpressionStatement:
	expr=Expression 
	';'
	;

// do not rename this rule (fragment parsing)	
Expression:	
	let=LetExpression
	|	expr=ImplicationExpression
	|   container=ContainerInitializer
	;

LetExpression:	
	'let' 
	type=Type name=Identifier // type is optional in OCL - IVML adjustment
	'=' 
	valueExpr=Expression 
	'in' 
	subExpr=OptBlockExpression
	;

OptBlockExpression:
    expr=Expression | block=BlockExpression
;
	
BlockExpression:
    '{'
    exprs+=ExpressionStatement+
    '}'
;	

AssignmentExpression:
    left=LogicalExpression
    right+=AssignmentExpressionPart?
;

AssignmentExpressionPart:
    op=AssignmentOperator
    (ex=LogicalExpression | container=ContainerInitializer)
;

AssignmentOperator:
    '=' // IVML addition
    ;
	
ImplicationExpression:
    left=AssignmentExpression
    right+=ImplicationExpressionPart*
    ;

ImplicationExpressionPart:
    op=ImplicationOperator
    ex=AssignmentExpression    
    ;

ImplicationOperator:
    'implies'
    |'iff'  // IVML addition
    ;

LogicalExpression:	
	left=EqualityExpression 
	right+=LogicalExpressionPart*
	;	

LogicalExpressionPart:
	op=LogicalOperator 
	ex=EqualityExpression
;

LogicalOperator:
	'and'
	|'or'
	|'xor'
	;

EqualityExpression:   
    left=RelationalExpression 
    right=EqualityExpressionPart?
    ;   

EqualityExpressionPart:
    op=EqualityOperator
    (ex=RelationalExpression | container=ContainerInitializer)
;

EqualityOperator:
    '=='
    |'<>'
    |'!='
    ;

RelationalExpression:	
	left=AdditiveExpression 
	(right=RelationalExpressionPart right2=RelationalExpressionPart?)?
	;	

RelationalExpressionPart:
	op=RelationalOperator
	ex=AdditiveExpression
;

RelationalOperator:
	'>'
	|'<'
	|'>='
	|'<='
	;

AdditiveExpression:	
	left=MultiplicativeExpression 
	right+=AdditiveExpressionPart*
	;

AdditiveExpressionPart:
	op=AdditiveOperator 
	ex=MultiplicativeExpression
;

AdditiveOperator:
	'+'
	|'-'
	;
	
MultiplicativeExpression:	
	left=UnaryExpression 
	right=MultiplicativeExpressionPart?
	;

MultiplicativeExpressionPart:
	op=MultiplicativeOperator
	expr=UnaryExpression
;

MultiplicativeOperator:
	'*'
	|'/'
	;

UnaryExpression:	
	op = UnaryOperator?
	expr=PostfixExpression
	;

UnaryOperator:
	'not'
	|'-'
	;	

PostfixExpression:	
	(call=FeatureCall (fCalls+=Call)* access=ExpressionAccess?) // IVML addition, call primitive operations
	| (left=PrimaryExpression)
	;

Call: // IVML: "lambda"-expression with apply operation set->apply(t,r=0|max(t,r))
	'.' call=FeatureCall 
	| '->' containerOp=ContainerOp 
	| '[' arrayEx=Expression ']' // IVML addition to OCL
;

FeatureCall:	
	(name=Identifier|name=RelationalOperator|name=AdditiveOperator|name=MultiplicativeOperator|name=EqualityOperator|
	     name=ImplicationOperator|name=LogicalOperator|name="not")
	'(' 
	ActualArgumentList? 
	')'
	;
	
ContainerOp:
	name=Identifier 
	'(' 
		decl=Declarator? ActualArgumentList?
	')'
	;

Declarator:
	decl+=Declaration (';' decl+=Declaration)* '|'
	;

Declaration:	
	(type=Type)? id+=Identifier (',' id+=Identifier)*
	('=' init=Expression)?
	;	
	
fragment ActualArgumentList:	
	args+=ActualArgument
	(
		',' 
		args+=ActualArgument
	)*
	;

ActualArgument:
    (name=Identifier '=')? arg=Expression    
;

ExpressionAccess:
	'.' name=Identifier
	calls+=Call*
	access=ExpressionAccess?
;

PrimaryExpression:	
	(lit=Literal
//	|	call=FeatureCall // unclear on what to execute (in OCL this is the context)
	|	'(' ex=Expression ')'
	|	ifEx=IfExpression
	|   'refBy' '(' refEx=Expression ')')
	calls+=Call*
	access=ExpressionAccess?
	;

ContainerInitializer:	
	{ContainerInitializer}
	type=QualifiedName? // not OclCollection as otherways Collections are excluded
	'{' 
	init=ExpressionListOrRange? 
	'}'
	;
 
ExpressionListOrRange:	// adjust values to Expressions
	list+=ExpressionListEntry 
	(',' list+=ExpressionListEntry)* 
//	|	'..' rangeEndEx=ExpressionListEntry // IVML change to OCL
	;
	
ExpressionListEntry:
	(name=Identifier ('.' attrib=Identifier)? '=')? // for compound initialization (IVML extension to OCL)
	(value=ImplicationExpression | container=ContainerInitializer)
	;	

Literal:	
	//Identifier | numValue
	val=Value
	;

IfExpression:	
	'if' 
	ifEx=Expression 
	'then' 
	thenEx=OptBlockExpression
	'else' 
	elseEx=OptBlockExpression
	'endif'
	;
	
// unify distinct terminals -> IdentifierValueConverter
// never allow identifiers starting with $ -> Reasoner name prefixes
Identifier:
    ID | VERSION | EXPONENT | 'version'
;

// --------------------- Terminals ----------------------------------
   
// otherwise we would have to write 0 . 5 instead of 0.5
terminal VERSION:
	'v' ('0'..'9')+ ('.' ('0'..'9')+)*
	;

// adjust de.uni_hildesheim.sse.model.validation.IvmlIdentifierCheck
// never allow identifiers starting with $ -> Reasoner name prefixes
terminal ID: 
	('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*
	;

terminal NUMBER:
    '-'?
    (('0'..'9')+ ('.' ('0'..'9')* EXPONENT?)?
    |   '.' ('0'..'9')+ EXPONENT?
    |   ('0'..'9')+ EXPONENT)
    ;

terminal EXPONENT: // just return a string
    ('e'|'E') ('+'|'-')? ('0'..'9')+
;
			
terminal STRING	: 
	'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
	"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
	; 

// do not change the name of this terminal!!! -> comment recognition
terminal ML_COMMENT: 
	'/*' -> '*/'
	;

// do not change the name of this terminal!!! -> comment recognition
terminal SL_COMMENT: 
	'//' !('\n'|'\r')* ('\r'? '\n')?
	;

// do not change the name of this terminal!!! -> comment recognition
terminal WS: 
	(' '|'\t'|'\r'|'\n')+
	;

terminal ANY_OTHER: 
	.
	;
