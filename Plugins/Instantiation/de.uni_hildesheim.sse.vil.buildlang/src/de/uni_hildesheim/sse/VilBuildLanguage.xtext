grammar de.uni_hildesheim.sse.VilBuildLanguage with de.uni_hildesheim.sse.vil.expressions.ExpressionDsl

generate vilBuildLanguage "http://www.uni_hildesheim.de/sse/VilBuildLanguage"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

ImplementationUnit:
    {ImplementationUnit}
    imports+=Import*
    requires+=Require*
    scripts+=LanguageUnit*;

Require:
    //here fqn because this may reference a specific project (of an external project)
    'requireVTL' name=STRING versionSpec=VersionSpec
    ';'
;

// ---------------- project header

LanguageUnit: // do not rename - required for reuse
    (advices+=Advice)*
    'vilScript' name=Identifier 
    '(' param=ParameterList? ')'
    (parent=ScriptParentDecl)?
    '{'
       version=VersionStmt?
       loadProperties+=LoadProperties*
       contents=ScriptContents
    '}' ';'?
;

ScriptParentDecl:
    'extends' name=Identifier //here identifier because this references a complete project
;

LoadProperties:
    'load' 'properties' path=STRING // here string because this references a file
    ';'
;

ScriptContents:
    {ScriptContents} // ensure object creation
    (
        elements+=VariableDeclaration
        | elements+=Compound
        | elements+=TypeDef
        | elements+=RuleDeclaration
    )*
;

// ---------------- rules

RuleDeclaration:
    {RuleDeclaration}
    (
        (modifier = RuleModifier)?
        (type = Type)?
        name=Identifier
        '(' (paramList=ParameterList)? ')'
        '='
    )?
    conditions = RuleConditions?
    block=RuleElementBlock
    ';'?
;

RuleConditions:
    {RuleConditions}
    (postcondition+=LogicalExpression)?
    ':'
    (preconditions+=LogicalExpression (',' preconditions+=LogicalExpression)*)?
;

RuleElementBlock:
    {RuleElementBlock}
    '{'
       elements+=RuleElement*
    '}'
;

RuleElement:
    varDecl=VariableDeclaration
    | exprStmt=ExpressionStatement
    | while=While ';'?
    | for=For ';'?
;

RuleModifier:
    protected = 'protected'
;

// ----------------------- overriding and extending parts of the expression grammar -------------------

ExpressionStatement:
    (
      (var=Identifier ('.' field=Identifier)? '=')? 
       expr=Expression ';') 
    | alt=Alternative ';'?
    ;

PrimaryExpression:  
    otherEx=ExpressionOrQualifiedExecution 
    | unqEx=UnqualifiedExecution 
    | superEx=SuperExecution 
    | sysEx=SystemExecution
    | map=Map
    | join=Join
    | instantiate=Instantiate
    | newEx=ConstructorExecution
    ;
    
Instantiate:
    'instantiate' 
    (project=Identifier | ruleName=STRING)
    '(' param=ArgumentList? ')' (versionSpec=VersionSpec?)
;

LoopVariable:
    type=Type?
    var=Identifier
;

Map:
    'map' '(' var+=LoopVariable (',' var+=LoopVariable)* separator=('=' | ':') expr=Expression ')'
    block=RuleElementBlock
;

For:
    'for' '(' var+=LoopVariable (',' var+=LoopVariable)* separator=('=' | ':') expr=Expression ')'
    block=RuleElementBlock
;

While:
    'while' '(' expr=Expression ')'
    block=RuleElementBlock
;

Alternative:
    'if' '(' expr=Expression ')' if=StatementOrBlock (=> 'else' else=StatementOrBlock)?
;
    
StatementOrBlock:
    exStmt = ExpressionStatement | block = RuleElementBlock
;

Join: 
    'join' '(' 
        var1=JoinVariable ',' 
        var2=JoinVariable ')' 
        ('with' 
            '(' condition=Expression ')'
        )? 
    ;

JoinVariable:
    (excl='exclude'?)
    var=Identifier
    ':'
    expr=Expression
    ;

SystemExecution:
    'execute' call=Call calls+=SubCall*
    ;
    